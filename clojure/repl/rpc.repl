(ns repl.rpc
  (:refer-clojure :exclude [send])
  (:require
   [clojure.java.io :as io]
   [clojure.spec.alpha :as spec]
   [cognitect.transcriptor :as xr]
   [tutkain.rpc :as rpc]
   [tutkain.rpc.test :refer [string->base64]]
   [tutkain.socket :as socket]
   [tutkain.test]))

(def eval-lock (Object.))

(def backchannel
  (rpc/open {:port 1337 :xform-in #(assoc % :eval-lock eval-lock :eval-future (atom nil))}))

(xr/on-exit #(rpc/close backchannel))
(def client (socket/client :host "localhost" :port (rpc/port backchannel)))
(xr/on-exit #((:stop client)))

(defn send
  [message]
  ((:send client) message))

(defn recv
  []
  ((:recv client)))

(defn val?
  [val]
  (fn [ret]
    (#{{:tag :ret :val (str val \newline)}
       {:tag :ret :val (str val \return \newline)}} ret)))

(defn out?
  [val]
  (fn [ret]
    (#{{:tag :out :val (str val \newline)}
       {:tag :out :val (str val \return \newline)}} ret)))

(send {:op :echo})

(recv)
(xr/check! #{{:op :echo}})

(defmethod rpc/handle :error
  [_]
  (throw (ex-info "Boom!" {:data :data})))

(spec/def ::tag #{:ret})
(spec/def ::exception? true?)
(spec/def ::val string?)

(spec/def ::ret
  (spec/keys :req-un [::tag ::exception ::val]))

(send {:op :error})
(recv)
(xr/check! ::ret)

;; Backchannel continues to work after handler throws an error
(send {:op :echo})
(recv)

;; Backchannel has access to the same bindings as the REPL thread
(send {:op :test
       :ctx (string->base64 "(ns my.test (:require [clojure.test :refer [deftest is]]))
(deftest test-assert (is (true? *assert*)))")
       :file "/path/to/my/test.clj"
       :code (string->base64 "(ns my.test (:require [clojure.test :refer [deftest is]]))
(deftest test-assert (is (true? *assert*)))")})

(spec/def ::fail empty?)
(spec/def ::type #{:pass})
(spec/def ::line #{2})
(spec/def ::pass (spec/coll-of (spec/keys :req-un [::type ::line])))

(spec/def ::test-results
  (spec/keys :req-un [::fail ::pass ::error]))

(recv)
(xr/check! ::test-results)

;; Resolving a absolute path relative to classpath root

(rpc/relative-to-classpath-root nil)
(xr/check! #{"NO_SOURCE_PATH"})

(rpc/relative-to-classpath-root "")
(xr/check! #{"NO_SOURCE_PATH"})

(rpc/relative-to-classpath-root "/does/not/exist.clj")
(xr/check! #{"/does/not/exist.clj"})

(rpc/relative-to-classpath-root
  (.getAbsolutePath (io/as-file (io/resource "tutkain/rpc.cljc"))))

(xr/check! #{"tutkain/rpc.cljc"})

;; eval
(send {:op :eval :code "(inc 1)"})
(recv)
(xr/check! (val? 2))

;; *1
(send {:op :eval :code "*1"})
(recv)
(xr/check! (val? 2))

;; print from within lazy seq
(send {:op :eval :code "(map prn (range 1))"})
(recv)
(xr/check! (val? '(nil)))
(recv)
(xr/check! (out? "0"))

(defn f [x] (/ 6 x))

;; ns
(send {:op :eval :code "(f 2)" :ctx (string->base64 "(ns repl.rpc)")})
(recv)
(xr/check! (val? 3))

;; file, line, column
(send {:op :eval :code "(f 0)" :ctx (string->base64 "(ns repl.rpc)") :file "/path/to/my.clj" :line 3 :column 4})
(recv)
(:val *1)
(xr/check! (partial re-matches #"(?s).*ArithmeticException.*"))

;; stack trace
(send {:op :eval :code "(Throwable->map *e)"})
(->> (recv) :val read-string :trace (drop 4) first (take-last 2))
(xr/check! #{'("my.clj" 3)})

;; in-ns
(send {:op :eval :code "(require 'clojure.set)"})
(recv)
(send {:op :eval :code "(in-ns 'clojure.set)"})
(recv)
(send {:op :eval :code "(ns-name *ns*)"})
(recv)
(xr/check! (val? "clojure.set"))

;; interrupt
(send {:op :eval :code "(Thread/sleep Integer/MAX_VALUE)"})
(send {:op :interrupt})

(def ret (recv))
(:val ret)
(xr/check! (partial re-matches #"(?s).*InterruptedException.*"))

;; exception printing
(send {:op :eval :code ":::5"})
(recv)
(:val *1)
(xr/check! (partial re-matches #"(?s)Syntax error reading source at \(REPL:\d+:\d+\)\.\r?\nInvalid token: :::5\r?\n"))

(send {:op :eval
       :ctx (string->base64 "(ns user)")
       :code "(deftype T [a]) (defmethod print-method T [_ w] (throw (Exception. \"boom\"))) (->T 1)"})
(recv)
(recv)
(recv)
(:val *1)
(xr/check! (partial re-matches #"(?s)Error printing return value at .+? \(NO_SOURCE_FILE:\d+\)\.\r?\nboom\r?\n"))

;; Namespace discovery
(defn str->reader
  [s]
  (-> s java.io.StringReader. clojure.lang.LineNumberingPushbackReader.))

;; ns
(rpc/ns-binder (str->reader "(ns foo.bar)"))
(xr/check! #{'(ns foo.bar)})

(rpc/ns-binder (str->reader "(ns ^:private foo.bar)"))
(xr/check! #{'(ns foo.bar)})

(rpc/ns-binder (str->reader "(ns ^{:doc \"\" :author \"John Doe\"} foo.bar)"))
(xr/check! #{'(ns foo.bar)})

(rpc/ns-binder (str->reader "(ns)"))
(xr/check! nil?)

;; in-ns
(rpc/ns-binder (str->reader "(in-ns 'foo.bar)"))
(xr/check! #{'(in-ns 'foo.bar)})
(rpc/ns-binder (str->reader "(in-ns)"))
(xr/check! nil?)

;; Syntax errors
(rpc/ns-binder (str->reader "(ns foo.bar"))
(xr/check! nil?)

(rpc/ns-binder (str->reader "(ns 'foo.bar"))
(xr/check! nil?)

(rpc/ns-binder (str->reader "(in-ns 'foo.bar"))
(xr/check! nil?)

(rpc/ns-binder (str->reader "(in-ns foo.bar)"))
(xr/check! nil?)

(rpc/ns-binder (str->reader "(ns foo.bar) (baz/)"))
(xr/check! #{'(ns foo.bar)})

;; seek
(rpc/ns-binder (str->reader "(ns foo.bar)\n(in-ns 'baz.quux)") :seek #{'ns})
(xr/check! #{'(ns foo.bar)})

(rpc/ns-binder (str->reader "(ns foo.bar)\n(in-ns 'baz.quux)") :seek #{'in-ns})
(xr/check! #{'(in-ns 'baz.quux)})

;; Multiple ns
(rpc/ns-binder (str->reader "(ns foo.bar)\n(ns baz.quux)") :line 1 :column 1)
(xr/check! nil?)

(rpc/ns-binder (str->reader "(ns foo.bar)\n(ns baz.quux)") :line 1 :column 13)
(xr/check! #{'(ns foo.bar)})

(rpc/ns-binder (str->reader "(ns foo.bar)\n(ns baz.quux)") :line 2 :column 1)
(xr/check! #{'(ns foo.bar)})

(rpc/ns-binder (str->reader "(ns foo.bar)\n(ns baz.quux)") :line 2 :column 13)
(xr/check! #{'(ns foo.bar)})

(rpc/ns-binder (str->reader "(ns foo.bar)\n(ns baz.quux)") :line 2 :column 14)
(xr/check! #{'(ns baz.quux)})

;; Multiple in-ns
(rpc/ns-binder (str->reader "(in-ns 'foo.bar)\n(in-ns 'baz.quux)") :line 1 :column 1)
(xr/check! nil?)

(rpc/ns-binder (str->reader "(in-ns 'foo.bar)\n(in-ns 'baz.quux)") :line 1 :column 17)
(xr/check! #{'(in-ns 'foo.bar)})

(rpc/ns-binder (str->reader "(in-ns 'foo.bar)\n(in-ns 'baz.quux)") :line 2 :column 1)
(xr/check! #{'(in-ns 'foo.bar)})

(rpc/ns-binder (str->reader "(in-ns 'foo.bar)\n(in-ns 'baz.quux)") :line 2 :column 17)
(xr/check! #{'(in-ns 'foo.bar)})

(rpc/ns-binder (str->reader "(in-ns 'foo.bar)\n(in-ns 'baz.quux)") :line 2 :column 18)
(xr/check! #{'(in-ns 'baz.quux)})

;; ns-form after other form

(rpc/ns-binder (str->reader "(inc 1)\n(ns foo.bar)\n(inc 2)\n(ns baz.quux)") :line 1 :column 1)
(xr/check! nil?)

(rpc/ns-binder (str->reader "(inc 1)\n(ns foo.bar)\n(inc 2)\n(ns baz.quux)") :line 2 :column 13)
(xr/check! #{'(ns foo.bar)})

(rpc/ns-binder (str->reader "(inc 1)\n(ns foo.bar)\n(inc 2)\n(ns baz.quux)") :line 4 :column 14)
(xr/check! #{'(ns baz.quux)})

;; ns-sym

(rpc/ns-sym '(in-ns 'foo.bar))
(xr/check! #{'foo.bar})

(rpc/ns-sym '(ns foo.bar))
(xr/check! #{'foo.bar})

(rpc/ns-sym '())
(xr/check! nil?)

(rpc/ns-sym '(ns ^:private foo.bar))
(xr/check! #{'foo.bar})

(rpc/ns-sym '(ns ^{:foo :bar} baz.quux))
(xr/check! #{'baz.quux})

;; context-ns

(rpc/context-ns (string->base64 "") 1 1)
(xr/check! #{'user})

(rpc/context-ns (string->base64 "(ns foo.bar)") 1 1)
(xr/check! #{'user}) ; I think this makes sense, but will it cause trouble?

(rpc/context-ns (string->base64 "(ns foo.bar)") 1 13)
(xr/check! #{'foo.bar})
