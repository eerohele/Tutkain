(ns repl.yodawg
  (:refer-clojure :exclude [send])
  (:require
   [clojure.core.server :as server]
   [clojure.spec.alpha :as spec]
   [cognitect.transcriptor :as xr]
   [tutkain.socket :as socket]
   [tutkain.repl :as repl]
   [tutkain.java])
  (:import
   (java.net ConnectException)))

(spec/def ::tag #{:ret :err :out :tap})
(spec/def ::val string?)
(spec/def ::ns string?)
(spec/def ::ms int?)
(spec/def ::form string?)

(spec/def ::ret
  (spec/keys :opt-un [::tag ::val ::ns ::ms ::form]))

(def server-name "yodawg")

(xr/on-exit #(server/stop-server server-name))

(def server
  (do
    (server/stop-server server-name)
    (server/start-server
      {:name server-name
       :port 0
       :accept `repl/repl
       :args [{:add-tap? true}]})))

(def repl (socket/client :host "localhost" :port (.getLocalPort server)))
(xr/on-exit #((:stop repl)))

(def backchannel-addr ((:recv repl)))

(def backchannel (socket/client :host (:host backchannel-addr) :port (:port backchannel-addr)))
(xr/on-exit #((:stop backchannel)))

(defn recv
  [channel]
  (case channel
    :repl ((:recv repl))
    :backchannel ((:recv backchannel))))

(defn send
  [channel message]
  (case channel
    :repl ((:send repl) message)
    :backchannel ((:send backchannel) message)))

(defn send-str
  [channel message]
  (case channel
    :repl ((:send-str repl) message)
    :backchannel ((:send backchannel) message)))

(def timeout? (partial = ::socket/timeout))

(recv :backchannel) ; Clojure version
(xr/check! ::ret)

;; Smoke test
(send :backchannel {:op :set-thread-bindings :line 1 :column 1 :id 1})
(recv :backchannel)
(send :repl '(inc 1))
(recv :repl)
(xr/check! #{2})

;; Sending more than one form at once
(send :backchannel {:op :set-thread-bindings :line 1 :column 1 :id 1})
(recv :backchannel)
(xr/check! ::ret)
(doto (:writer repl) (.write "(inc 1) (inc 2)\n") (.flush))
(recv :repl)
(xr/check! #{2})
(recv :repl)
(xr/check! #{3})

;; Check that (read-line) works
;;
;; https://clojure.atlassian.net/browse/CLJ-2692
(send :backchannel {:op :set-thread-bindings :line 1 :column 1 :id 1})
(recv :backchannel)
(send :repl '(read-line))
(recv :repl)
(xr/check! timeout?)
(send :repl 1)
(recv :repl)
(xr/check! #{"1"})

;; Exceptions
(send :backchannel {:op :set-thread-bindings :line 1 :column 1 :id 1})
(recv :backchannel)
(send :repl '(/ 4 0))
(recv :repl)
(xr/check! timeout?)
(recv :backchannel)
(xr/check! ::ret) ;; err

;; flush after eval
(send :backchannel {:op :set-thread-bindings :line 1 :column 1 :id 1})
(recv :backchannel)
(send :repl '(print "foo"))
(recv :repl)
(xr/check! nil?)
(recv :backchannel)
(xr/check! #{{:tag :out :val "foo"}})

;; flush after eval that throws
(send :backchannel {:op :set-thread-bindings :line 1 :column 1 :id 1})
(recv :backchannel)
(send :repl '(do (print "foo") (/ 4 0)))
(recv :repl)
(xr/check! timeout?)
(recv :backchannel)
(xr/check! ::ret) ;; err
(recv :backchannel)
(xr/check! (spec/or :out #{{:tag :out :val "foo"}} :err ::ret))

;; set ns before eval
(send :backchannel {:op :set-thread-bindings :line 1 :column 1 :id 1})
(recv :backchannel)
(xr/check! ::ret)
(send :repl `(require '[clojure.set]))
(recv :repl)
(xr/check! nil?)
;; no ns => ns auto switch disabled
(send :backchannel {:op :set-thread-bindings :line 1 :column 1 :id 1})
(recv :backchannel)
(send :repl `(in-ns 'clojure.set))
(recv :repl)
(xr/check! tagged-literal?)
(send :repl '(ns-name *ns*))
(recv :repl)
(xr/check! #{'clojure.set})
;; no ns => ns auto switch disabled
(send :backchannel {:op :set-thread-bindings :line 1 :column 1 :id 1})
(recv :backchannel)
(send :repl '(union #{1 2} #{2 3}))
(recv :repl)
(xr/check! set?)

;; create ns if it doesn't already exist
(send :backchannel {:op :set-thread-bindings :ns 'my.ns.one :line 1 :column 1 :id 1})
(recv :backchannel)
(xr/check! ::ret)
(send :repl '(ns-name *ns*))
(recv :backchannel)
(xr/check! timeout?)
(recv :repl)
(xr/check! #{'my.ns.one})

(send :backchannel {:op :set-thread-bindings :line 1 :column 1 :id 1})
(recv :backchannel)
(send :repl '(tap> {:foo :bar}))
(recv :repl)
(xr/check! true?)
(recv :backchannel)
(xr/check! ::ret) ;; tap

;; source file path in exception stack trace
(send :backchannel {:op :set-thread-bindings :file "/my/path/app.clj" :ns 'user :line 1 :column 1 :id 1})
(recv :backchannel)
(xr/check! ::ret)
(send :repl '(/ 4 0))
(recv :backchannel)
(send :backchannel {:op :set-thread-bindings :file "/my/path/app.clj" :ns 'user :line 1 :column 1 :id 1})
(recv :backchannel)
(send :repl '*e)
(recv :repl)
(nth (->> *1 .form :trace (drop 2) first) 2)
(xr/check! #{"app.clj"})

;; interrupts
(send :backchannel {:op :set-thread-bindings :line 1 :column 1 :id 1})
(recv :backchannel)
(send :repl '(Thread/sleep Integer/MAX_VALUE))
(send :backchannel {:op :interrupt})
(recv :backchannel)
(xr/check! ::ret) ;; InterruptedException

;; *1 works
(send :backchannel {:op :set-thread-bindings :line 1 :column 1 :id 1})
(recv :backchannel)
(send :repl '(inc 1))
(def ret (recv :repl))
(send :backchannel {:op :set-thread-bindings :line 1 :column 1 :id 1})
(recv :backchannel)
(send :repl '*1)
(recv :repl)
(xr/check! (partial = ret))

;; Switching namespace while previous eval is in progress

;; Create var in my.ns.one
(send :backchannel {:op :set-thread-bindings :ns 'my.ns.one :line 1 :column 1 :id 1})
(recv :backchannel)
(send :repl '(def f1 1))
(recv :repl)

;; Create var in baz.quux
(send :backchannel {:op :set-thread-bindings :ns 'my.ns.two :line 1 :column 1 :id 1})
(recv :backchannel)
(send :repl '(def f2 2))
(recv :repl)

;; Eval promise in my.ns.one
(send :backchannel {:op :set-thread-bindings :ns 'my.ns.one :line 1 :column 1 :id 1})
(recv :backchannel)
(send :repl '(def p (promise)))
(recv :repl)
(send :repl '(deref p))

;; While previous eval is in progress, eval f2 in my.ns.two
(send :backchannel {:op :set-thread-bindings :ns 'my.ns.two :line 1 :column 1 :id 1})
(recv :backchannel)
(send :repl 'f2)
(deliver my.ns.one/p :done)
(recv :repl)
(xr/check! #{:done})
(recv :repl)
(xr/check! #{2})

;; Backchannel eval

(send :backchannel
  {:op :eval
   :file "/my/path/app.clj"
   :line 1
   :column 1
   :code "(inc 1)"
   :id 1})

(recv :backchannel)
(xr/check! #{{:id 1 :tag :ret :val "2\n"}
             {:id 1 :tag :ret :val "2\r\n"}})

(send :backchannel
  {:op :eval
   :file "/my/path/app.clj"
   :line 3
   :column 4
   :code "(/ 4 0)"
   :id 2})

(recv :backchannel)
(xr/check! ::ret) ;; Error

;; Backchannel eval, multiple forms
(send :backchannel
  {:op :eval
   :file "/my/path/app.clj"
   :line 1
   :column 1
   :code "(inc 1) (inc 2)"
   :id 3})

(recv :backchannel)

(xr/check! #{{:id 3 :tag :ret :val "2\n"}
             {:id 3 :tag :ret :val "2\r\n"}})

(recv :backchannel)

(xr/check! #{{:id 3 :tag :ret :val "3\n"}
             {:id 3 :tag :ret :val "3\r\n"}})

;; Resolve stack trace

(spec/def ::line nat-int?)
(spec/def ::column nat-int?)

(spec/def ::name
  (spec/and string? (partial re-matches #".+/.+")))

(spec/def ::file-name string?)

(spec/def ::file string?)

(spec/def ::element
  (spec/keys :opt-un [::file] :req-un [::line ::column ::name ::file-name]))

(spec/def ::stacktrace
  (spec/coll-of ::element :min-count 1))

(send :backchannel {:op :resolve-stacktrace})
(recv :backchannel)

(:stacktrace *1)
(xr/check! ::stacktrace)

;; exception printing
(send :backchannel {:op :set-thread-bindings :line 1 :column 1 :id 1})
(recv :backchannel)
(send-str :repl ":::5")
(recv :backchannel)
(:val *1)
(xr/check! (partial re-matches #"(?s)Syntax error reading source at \(REPL:\d+:\d+\)\.\r?\nInvalid token: :::5\r?\n"))

(send-str :repl "(deftype T [a]) (defmethod print-method T [_ w] (throw (Exception. \"boom\"))) (->T 1)")
(recv :backchannel)
(:val *1)
(xr/check! (partial re-matches #"(?s)Error printing return value at .+? \(NO_SOURCE_FILE:\d+\)\.\r?\nboom\r?\n"))

(send :backchannel {:op :set-thread-bindings :line 1 :column 1 :id 1})
(recv :backchannel)
(send :repl :repl/quit)

(Thread/sleep 3000)

(try
  (socket/client
    :host (:host backchannel-addr)
    :port (:port backchannel-addr))
  false
  (catch ConnectException _
    ;; Can't connect to backchannel anymore after quitting the REPL
    true))

(xr/check! true?)
